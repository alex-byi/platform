MODULE Icon;

REQUIRE Utils, Reflection;

// Icon properties

CLASS Icon;
iconName = DATA STRING (Icon);
label = DATA STRING (Icon);
terms = DATA STRING (Icon);
synonyms = DATA STRING (Icon);
free = DATA STRING (Icon);
iconClass(Icon i) = (IF free(i) = 'brands' THEN 'fa-brands' ELSE 'fa') + ' fa-' + iconName(i);
vector(Icon i) = tsVector(label(i), terms(i), synonyms(i));
rank(STRING s, Icon i) = tsRank(vector(i), splitCamelCase(s, ' | '));

// Searching the best icon

bestIconClass = DATA LOCAL STRING();
bestIconRank = DATA LOCAL DOUBLE();
getBestIcon(STRING s) {
    FOR Icon lastIcon = (GROUP LAST Icon icn IF rank(s, icn) ORDER rank(s, icn)) DO {
        bestIconClass() <- iconClass(lastIcon);
        bestIconRank() <- rank(s, lastIcon);
    }
}

// Icon import

data_id_terms = DATA LOCAL STRING (INTEGER);
data_id_synonyms = DATA LOCAL STRING (INTEGER);
data_id_free = DATA LOCAL STRING (INTEGER);
term = DATA LOCAL STRING(INTEGER);
synonym = DATA LOCAL STRING(INTEGER);
free = DATA LOCAL STRING(INTEGER);
label = DATA LOCAL STRING(STRING);

FORM importIcons
    OBJECTS data=STRING EXTKEY

    PROPERTIES(data) label

    OBJECTS terms=INTEGER
    PROPERTIES(terms) term EXTID 'value'
    FILTERS data_id_terms(terms) == data

    OBJECTS synonyms=INTEGER
    PROPERTIES (synonyms) synonym EXTID 'value'
    FILTERS data_id_synonyms(synonyms) == data

    OBJECTS free=INTEGER
    PROPERTIES (free) free EXTID 'value'
    FILTERS data_id_free(free) == data
;

icon(iconName) = GROUP AGGR Icon icn BY iconName(icn);
terms(STRING iconName) = GROUP CONCAT term(INTEGER i) IF data_id_terms(i) == iconName, ' ' ORDER i;
synonyms(STRING iconName) = GROUP CONCAT synonym(INTEGER i) IF data_id_synonyms(i) == iconName, ' ' ORDER i;
free(STRING iconName) = GROUP CONCAT free(INTEGER i) IF data_id_free(i) == iconName, ' ' ORDER i;

iconName = GROUP MAX STRING s BY label(s);
iconsImport() {
    readResource('/web/bootstrap/classic/iconsSearch/icons_with_synonyms.json');
    IF resource() THEN {
        IMPORT importIcons JSON FROM resource();

        FOR iconName(STRING s) AND NOT icon(iconName(s)) NEW icn = Icon DO {
            iconName(icn) <- iconName(s);
        }

        FOR iconName(Icon icn) == iconName(STRING s) DO {
            label(icn) <- lower(s);
            terms(icn) <- lower(terms(iconName(icn)));
            synonyms(icn) <- lower(synonyms(iconName(icn)));
            free(icn) <-  free(iconName(icn));
        }

        DELETE Icon icn WHERE icn IS Icon AND NOT [ GROUP SUM 1 BY iconName(STRING s)](iconName(icn));

        APPLY;
    }
}

// Icons form

search = DATA LOCAL STRING ();
message(Icon i) { MESSAGE iconName(i); }
FORM icons '{navigator.icon}' 
    PROPERTIES() iconsImport, search ON CHANGE {
        INPUT s = search() CHANGE DO {
            getBestIcon(s);
        }
    }, bestIconClass  READONLY IMAGE bestIconClass(), bestIconRank READONLY 
    OBJECTS i = Icon 
    PROPERTIES iconImage 'Icon' = message(i) IMAGE iconClass(i) GRID, rank 'Rank' = rank(search(), i) 
    PROPERTIES(i) READONLY iconName, label, terms, synonyms, free, vector
    PROPERTIES (i) DELETE
;

DESIGN icons {
    OBJECTS {
        NEW import {
            MOVE PROPERTY(iconsImport()) {
                caption = 'import JSON';
                fontSize = 36;
            }
        }
        NEW search {
            MOVE PROPERTY(search()) {
                caption = 'action name';
                fontSize = 36;
                width = 500;
            }
        }
        NEW bestIcon {
            MOVE PROPERTY(bestIconClass()) {
                caption = 'best icon name';
                fontSize = 36;
                width = 500;
            }
            MOVE PROPERTY(bestIconRank()) {
                caption = 'best icon rank';
                fontSize = 36;
                width = 500;
            }
        }
        NEW icons {
            fill = 1;
            MOVE BOX(i);
        }
    }
}

NAVIGATOR {
    system {
        NEW FOLDER icon '{navigator.icon}' AFTER metadata {
            NEW icons;
        }
    }
}