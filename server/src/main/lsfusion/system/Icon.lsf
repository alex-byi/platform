MODULE Icon;

REQUIRE Utils, Reflection;

// Icon properties

TABLE icon () NODEFAULT;
type(STRING styles) = IF styles = 'bi' THEN 'bi' ELSE ('fa' IF styles IS STRING); 

CLASS Icon;
name '{icon.name}' = DATA STRING (Icon);
explicit = DATA STRING (Icon);
label '{icon.label}' = DATA STRING (Icon);
terms '{icon.terms}' = DATA STRING (Icon);
synonyms '{icon.synonyms}' = DATA STRING (Icon);
styles '{icon.free.class}' = DATA STRING (Icon);

type(Icon i) = type(styles(i)) MATERIALIZED;
INDEX name(Icon i), type(i);
icon = GROUP AGGR Icon icn BY name(icn), type(icn) MATERIALIZED INDEXED;

searchStyles(Icon i) = IF type(i) = 'bi' THEN 'lsfreg'
                              ELSE IF styles(i) = 'brands' THEN '' 
                              ELSE 'lsfsol' + (IF styles(i) LIKE '%regular%' THEN ' lsfreg' ELSE ('' IF i IS Icon)) MATERIALIZED;  
iconClass(STRING s, Icon i) = IF type(i) = 'bi' THEN 'bi bi-' + name(i)
                              ELSE ((IF styles(i) = 'brands' THEN 'fa-brands'
                              ELSE (IF endsWith(s, 'Lsfreg') AND styles(i) LIKE '%regular%' THEN 'fa-regular' ELSE 'fa')) + ' fa-' + name(i)); // should correspond AppServerImage.createDefaultImage -> getBestIcon param

vector '{icon.vector}' (Icon i) = toTsVector(CONCAT ' ', explicit(i), label(i), terms(i), synonyms(i), searchStyles(i)) MATERIALIZED INDEXED MATCH;
nameToIconQuery(STRING s) = toTsQuery(splitCamelCase(s, ' | '));

tsRank(Icon i, TSQUERY query) =
                tsRank(toTsVector(explicit(i)), query) * 8 +
                tsRank(toTsVector(label(i)), query, 16) * 4 +
                tsRank(toTsVector(terms(i)), query) * 2 +
                tsRank(toTsVector(synonyms(i)), query, 16) +
                tsRank(toTsVector(searchStyles(i)), query) * 1.5;

// Searching the best icon

bestIconNames = DATA LOCAL BOOLEAN (STRING); // input
bestIconClasses = DATA LOCAL STRING (STRING); // output
bestIconRanks = DATA LOCAL DOUBLE (STRING); // output
getBestIcons() {
    // materializing tsqueries since postgres won't do that
    LOCAL bestIconMatchQueries = TSQUERY (STRING);
    LOCAL bestIconRankQueries = TSQUERY (STRING);
    bestIconMatchQueries(STRING s) <- nameToIconQuery(left(s, length(s) - 6)) WHERE bestIconNames(s);  // should correspond AppServerImage.createDefaultImage -> getBestIcon param
    bestIconRankQueries(STRING s) <- nameToIconQuery(s) WHERE bestIconNames(s);
    
    FOR TSQUERY mq = bestIconMatchQueries(STRING s) AND TSQUERY rq = bestIconRankQueries(s) AND Icon lastIcon = (GROUP LAST Icon icn IF vector(icn) MATCH mq ORDER tsRank(icn, rq)) DO {
        bestIconClasses(s) <- iconClass(s, lastIcon);
        bestIconRanks(s) <- tsRank(lastIcon, rq);
    }
}
 

// Icon import

term = DATA LOCAL STRING(INTEGER);
termIcon = DATA LOCAL INTEGER (INTEGER);
synonym = DATA LOCAL STRING(INTEGER);
synonymIcon = DATA LOCAL INTEGER (INTEGER);
style = DATA LOCAL STRING(INTEGER);
styleIcon = DATA LOCAL INTEGER (INTEGER);

label = DATA LOCAL STRING(INTEGER);
name = DATA LOCAL STRING(INTEGER);

FORM importIcons
    OBJECTS value = INTEGER 

    PROPERTIES(value) label, name

    OBJECTS terms=INTEGER
    PROPERTIES(terms) term EXTID 'value'
    FILTERS termIcon(terms) = value

    OBJECTS synonyms=INTEGER
    PROPERTIES (synonyms) synonym EXTID 'value'
    FILTERS synonymIcon(synonyms) = value

    OBJECTS free=INTEGER
    PROPERTIES (free) style EXTID 'value'
    FILTERS styleIcon(free) = value
;

terms = GROUP CONCAT term(INTEGER i), ' ' ORDER i BY termIcon(i);
synonyms = GROUP CONCAT synonym(INTEGER i) , ' ' ORDER i BY synonymIcon(i);
styles = GROUP CONCAT style(INTEGER i), ' ' ORDER i BY styleIcon(i);
type (INTEGER i) = type(styles(i));

importIconsHash = DATA STRING () TABLE icon; // to avoid update conflicts

importIcons '{icon.import.json}' () {
    
    APPLY {
        readResource('/web/icons_with_synonyms.json');
        
        LOCAL newHash = STRING ();
        newHash() <- md5(resource());
        IF NOT newHash() = importIconsHash() THEN {
            IMPORT importIcons JSON FROM resource();

            FOR name(INTEGER i) AND NOT icon(name(i), type(i)) INLINE NEW icn = Icon DO {
                name(icn) <- name(i);
                styles(icn) <- styles(i); // need this to fill the type
            }

            FOR name(Icon icn) = name(INTEGER i) AND type(icn) = type(i) INLINE DO {
                label(icn) <- lower(label(i));
                terms(icn) <- lower(terms(i));
                synonyms(icn) <- lower(synonyms(i));
                styles(icn) <- styles(i);
            }

            DELETE Icon icn WHERE icn IS Icon AND NOT [GROUP SUM 1 BY name(INTEGER i), type(i)](name(icn), type(icn));

            importIconsHash() <- newHash();
        }
    }

    // updating properties
    LOCAL allProperties = STRING (STRING);
    readResourcePaths('/[^/]*Icons\\.properties');
    FOR resourcePaths(STRING path) DO {
        readResource(path);
        
        readProperties(RAWFILE(resource()));
        
        allProperties(STRING s) <- properties(s) WHERE properties(s);        
    }

    explicit(Icon icn) <- [GROUP CONCAT splitCamelCase(STRING s AS STRING, ' ') , ' ' ORDER s BY allProperties(s)](name(icn));

    APPLY;
}

// Icons form

search '{icon.action.name}' = DATA LOCAL STRING ();
FORM icons '{navigator.icon}' 
    PROPERTIES() importIcons, search ON CHANGE {
        INPUT s = search() CHANGE DO {
            bestIconNames(s) <- TRUE;
            getBestIcons();
        }
    } 
    PROPERTIES bestIconClass '{icon.best.icon.name}' = bestIconClasses(search())  READONLY IMAGE bestIconClasses(search()), bestIconRank '{icon.best.icon.rank}' = bestIconRanks(search()) READONLY 
    OBJECTS i = Icon 
    PROPERTIES iconImage '{icon.icon}' = {} IMAGE iconClass(search(), i) GRID, rank '{icon.rank}' = tsRank(i, nameToIconQuery(search()))
    PROPERTIES =tsExplicitRank(i, nameToIconQuery(search())), =tsLabelRank(i, nameToIconQuery(search())), =tsTermsRank(i, nameToIconQuery(search())), =tsSynonymsRank(i, nameToIconQuery(search())), =tsSearchStylesRank(i, nameToIconQuery(search()))
    PROPERTIES(i) READONLY name, explicit, label, terms, synonyms, styles, vector
    PROPERTIES(i) DELETE
;

DESIGN icons {
    OBJECTS {
        NEW import {
            MOVE PROPERTY(importIcons()) {
                fontSize = 36;
            }
        }
        NEW search {
            MOVE PROPERTY(search()) {
                fontSize = 36;
                width = 500;
            }
        }
        NEW bestIcon {
            MOVE PROPERTY(bestIconClass) {
                fontSize = 36;
                width = 500;
            }
            MOVE PROPERTY(bestIconRank) {
                fontSize = 36;
                width = 500;
            }
        }
        NEW icons {
            fill = 1;
            MOVE BOX(i);
        }
    }
}

NAVIGATOR {
    system {
        NEW icons AFTER metadata;
    }
}

// Icons test form

CLASS Query;
text = DATA STRING (Query);

mrank(Icon i, STRING s) = tsRank(i, nameToIconQuery(s));

iconRank(Query q, Icon i) = mrank(i, text(q));
bestIcon(Query q) = GROUP LAST Icon i IF vector(i) MATCH nameToIconQuery(text(q)) ORDER mrank(i, text(q));
bestIconRank(Query q) = iconRank(q, bestIcon(q));
bestIconClass(Query q) = iconClass(text(q), bestIcon(q)); 

tsQuery(Query q) = nameToIconQuery(text(q));
tsQueryStr(Query q) = STRING(tsQuery(q));

queryValue = DATA INTEGER (Query);

sum = GROUP SUM queryValue(Query q) IF q IS Query;
countAll = GROUP SUM 1 IF Query q IS Query AND queryValue(q) > 0;
sum02 = GROUP SUM queryValue(Query q) IF q IS Query AND bestIconRank(q) >= 0.2;
count02 = GROUP SUM 1 IF Query q IS Query AND queryValue(q) > 0 AND bestIconRank(q) >= 0.2;
sum05 = GROUP SUM queryValue(Query q) IF q IS Query AND bestIconRank(q) >= 0.5;
count05 = GROUP SUM 1 IF Query q IS Query AND queryValue(q) > 0 AND bestIconRank(q) >= 0.5;

FORM icons_test 
    OBJECTS q = Query
    PROPERTIES 'Icon' = {} IMAGE bestIconClass(q) GRID
    PROPERTIES(q) VALUE, text, bestIconRank, queryValue, tsQueryStr, NEW, DELETE 
    
    OBJECTS i = Icon
    PROPERTIES iconImage '{icon.icon}' = {} IMAGE iconClass(text(q), i) GRID, rank '{icon.rank}' = iconRank(q, i)
    PROPERTIES(i) READONLY label, terms, synonyms, styles, vector
    PROPERTIES (i) DELETE
    
    PROPERTIES sum(), countAll(), sum02(), count02(), sum05(), count05()
    ORDERS rank DESC
    FILTERS iconRank(q, i) > 0
;

NAVIGATOR {
    system {
        NEW icons_test;
    }
}

// icon utility properties

badge(STRING badgeText) = HTML('$I{badge.html}');
// usage example: imaged($M{myimage},'mytext', TRUE, TRUE)
imaged(STRING imageHTML, STRING text, BOOLEAN vertical, BOOLEAN start) = HTML('$I{imaged.html}');
badged(STRING badgeText, STRING text, BOOLEAN vertical, BOOLEAN start) = imaged(badge(badgeText), text, vertical, start);

imaged(STRING imageHTML, STRING text) = imaged(imageHTML, text, NULL, TRUE);
badged(STRING text, STRING badgeText) = badged(badgeText, text, NULL, NULL);

badged(STRING text, INTEGER badgeNumber) = badged(text, STRING(badgeNumber));

// plugin bug
//imaged(STRING imageHTML, STRING text, BOOLEAN vertical) = '<div class = "${IF text != \'\' THEN \'wrap-text-not-empty \' ELSE \'\'}${IF imageHTML THEN \'wrap-img-\' + (IF vertical THEN \'vert\' ELSE \'horz\'})">\r\n    ${CONCAT \'\', imageHTML, text}\r\n</div>\r\n'

imagedCaption(StaticObject object) = imaged(image(object), staticCaption(object));